plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.8.17'
    id 'application' // New for distribution tasks
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.44.0'
    implementation 'io.grpc:grpc-protobuf:1.44.0'
    implementation 'io.grpc:grpc-stub:1.44.0'
    implementation 'io.grpc:grpc-services:1.44.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    implementation 'org.mongodb:mongodb-driver-sync:4.1.1' //TODO: Check if the usage of "Implementation" is really correct here!
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.44.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

// Added to deploy, following AdService

startScripts.enabled = false

task reviewService(type: CreateStartScripts) {
    mainClassName = 'review.ReviewService'
    applicationName = 'ReviewService'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
    defaultJvmOpts =
            ["-Dlog4j2.contextDataInjector=io.opencensus.contrib.logcorrelation.log4j2.OpenCensusTraceContextDataInjector",
             "-agentpath:/opt/cprof/profiler_java_agent.so=-cprof_service=adservice,-cprof_service_version=1.0.0"]
}

task reviewServiceClient(type: CreateStartScripts) {
    mainClassName = 'review.ReviewServiceClient'
    applicationName = 'ReviewServiceClient'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
    defaultJvmOpts =
            ["-Dlog4j2.contextDataInjector=io.opencensus.contrib.logcorrelation.log4j2.OpenCensusTraceContextDataInjector",
             "-agentpath:/opt/cprof/profiler_java_agent.so=-cprof_service=adserviceclient,-cprof_service_version=1.0.0"]
}

applicationDistribution.into('bin') {
    from(reviewService)
    from(reviewServiceClient)
    fileMode = 0755
}